options
{
	 JDK_VERSION = "1.5";
 	static = false;
 	MULTI=true;
 	VISITOR=true;
}

PARSER_BEGIN(WhereParser)
package com.monql.grammar;

import java.io.StringReader;
import java.io.Reader;

public class WhereParser {

	public WhereParser(String s)
    {
        this((Reader)(new StringReader(s)));

    }
}

PARSER_END(WhereParser)

SKIP :
{
	 " "
	| "\t"
	| "\r"
	| "\n"
}

TOKEN:
{
		<AND: "and">
	|  <OR:  "or">
	|  <LPAREN: "(">
	|  <RPAREN: ")">
	|  <EQUALS: "=">
	|  <NE: "!=">
	|  <LT: "<" >
	|  <LTE: "<=" >
	|  <GT: ">" >
	|	<GTE: ">=" >
	|  <IN: "in" >
	|  <ALL: "all" >
	|  <EXISTS: "exists" >
}

TOKEN :
{
  		  <KEYWORD: (~[" ","\t","\r","\n",":","(",")"]) ( ~[" ","\t","\r","\n"] )* >
	| 	 < PARAM_NUM: ":"["1"-"9"] >
}

SimpleNode parse()  #RootNode :
{
}
{
    expression()   <EOF>
    {
		return(SimpleNode) jjtThis.children[0];
    }
}

void expression() #OrNode(>1) :
{}
{
 	andExpression()  ( <OR> andExpression() )*
}

void andExpression() #AndNode(>1) :
{}
{
	termOrExpression() ( <AND> termOrExpression() )*
}

void termOrExpression()  #void :
{
}
{
    	term()
        |
        <LPAREN> expression() <RPAREN>
}

void term()  #TermNode :
{
	Token key = null;
	Token op = null;
	Token paramNum = null;
}
{
    	key=<KEYWORD>
        ( op=<EQUALS> | op=<NE> | op=<LT> | op=<LTE> | op=<GT> | op=<GTE> | op=<IN> | op=<ALL> | op=<EXISTS> )
        ( paramNum=<PARAM_NUM>)
        {
         	jjtThis.setKey(key.image);
			jjtThis.setOperatorFromOp(op.image);
			jjtThis.setParamNum(paramNum.image);
        }
}
